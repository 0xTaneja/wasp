app waspAi {
  wasp: {
    version: "^0.11.0"
  },
  title: "GPT Web App Generator ✨",
  head: [
    "<meta property=\"og:title\" content=\"GPT Web App Generator ✨\">",
    "<meta property=\"og:description\" content=\"Generate your full-stack React, Node.js and Prisma web app using the magic of GPT and the Wasp full-stack framework.\">",
    "<meta property=\"og:type\" content=\"website\">",
    "<meta property=\"og:image\" content=\"/cover.png\">"
  ],
  dependencies: [
    ("prismjs", "^1.29.0"),
    ("react-accessible-treeview", "2.6.1"),
    ("react-icons", "4.9.0"),
    ("@zip.js/zip.js", "2.7.16"),
    ("async-mutex", "0.4.0"),
    ("@headlessui/react", "1.7.15"),
    ("@heroicons/react", "2.0.18"),
    ("react-parallax-tilt", "1.7.151"),
    ("timeago.js", "4.0.2"),
    ("@visx/mock-data", "3.0.0"),
    ("@visx/group", "3.0.0"),
    ("@visx/shape", "3.0.0"),
    ("@visx/scale", "3.2.0"),
    ("@visx/responsive", "3.0.0"),
    ("@visx/gradient", "3.0.0"),
    ("@visx/axis", "3.2.0")
  ],
  client: {
    rootComponent: import { RootComponent } from "@client/RootComponent.jsx",
  },
  db: {
    system: PostgreSQL
  }
}

route RootRoute { path: "/", to: MainPage }
page MainPage {
  component: import Main from "@client/pages/MainPage.jsx"
}

route ResultRoute { path: "/result/:appId", to: ResultPage }
page ResultPage {
  component: import { ResultPage } from "@client/pages/ResultPage.jsx"
}

route StatsRoute { path: "/stats", to: StatsPage }
page StatsPage {
  component: import { Stats } from "@client/pages/StatsPage.jsx"
}

action startGeneratingNewApp {
  fn: import { startGeneratingNewApp } from "@server/operations.js",
  entities: [
    Project,
    File,
    Log,
  ]
}

query getAppGenerationResult {
  fn: import { getAppGenerationResult } from "@server/operations.js",
  entities: [
    Project
  ]
}

query getStats {
  fn: import { getStats } from "@server/operations.js",
  entities: [
    Project
  ]
}

entity Project {=psl
  id String @id @default(uuid())
  name String
  description String
  primaryColor String @default("sky")
  authMethod String @default("usernameAndPassword")
  createdAt DateTime @default(now())
  status String @default("in-progress")
  files File[]
  logs Log[]
psl=}

entity File {=psl
  id String @id @default(uuid())
  name String
  content String
  createdAt DateTime @default(now())
  projectId String
  project Project @relation(fields: [projectId], references: [id])

  @@index([name, projectId])
psl=}

entity Log {=psl
  id String @id @default(uuid())
  content String
  createdAt DateTime @default(now())
  projectId String
  project Project @relation(fields: [projectId], references: [id])
psl=}
