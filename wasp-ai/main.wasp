app waspAi {
  wasp: {
    version: "^0.11.0"
  },
  title: "wasp-ai",
  dependencies: [
    ("uuid", "^9.0.0"),
    ("prismjs", "^1.29.0"),
    ("react-accessible-treeview", "2.6.1"),
    ("react-icons", "4.9.0"),
    ("@zip.js/zip.js", "2.7.16")
  ],
  client: {
    rootComponent: import { RootComponent } from "@client/RootComponent.jsx",
  },
  db: {
    system: PostgreSQL
  }
}

route RootRoute { path: "/", to: MainPage }
page MainPage {
  component: import Main from "@client/pages/MainPage.jsx"
}

route ResultRoute { path: "/result/:appId", to: ResultPage }
page ResultPage {
  component: import { ResultPage } from "@client/pages/ResultPage.jsx"
}

action startGeneratingNewApp {
  fn: import { startGeneratingNewApp } from "@server/operations.js",
  entities: [
    Project,
    File,
    Log,
  ]
}

query getAppGenerationResult {
  fn: import { getAppGenerationResult } from "@server/operations.js",
  entities: [
    Project
  ]
}

entity Project {=psl
  id String @id @default(uuid())
  name String
  description String
  createdAt DateTime @default(now())
  files File[]
  logs Log[]
psl=}

entity File {=psl
  id String @id @default(uuid())
  name String
  content String
  createdAt DateTime @default(now())
  projectId String
  project Project @relation(fields: [projectId], references: [id])

  @@index([name, projectId])
psl=}

entity Log {=psl
  id String @id @default(uuid())
  content String
  createdAt DateTime @default(now())
  projectId String
  project Project @relation(fields: [projectId], references: [id])
psl=}
